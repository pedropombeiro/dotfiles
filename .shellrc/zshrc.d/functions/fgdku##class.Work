#!/usr/bin/env zsh

#
# `fgdku` takes care of updating the GDK taking certain precautions:
# - removes .git/gc.log
# - restores bin/rake bin/rspec bin/rails bin/spring
# - restores db/structure.sql in case it is out of sync
# - restarts the GDK if the Redis socket disappeared
# - undoes any migrations from the active branch
# - if the Redis socket is still missing, brings GDK back to pristine state
# - precompiles static assets to ensure graphiql-rails changes are picked up
# - prunes and rebases local branches, taking care to preserve branch dependencies
# - ensures there is no orphaned Sidekiq cluster
# - reapplies migrations from active branch
# - reapplies spring stubs
# - runs a simple test in order to bring up Gitaly
# - activates RubyMine if it is running so it can update its indices in the background
# - performs a web request in order to warm up web server
#

source ~/.config/yadm/scripts/colors.sh

(
  cd "${GDK_ROOT}/gitlab" || exit 0

  git restore db/structure.sql bin/rake bin/rspec bin/rails bin/spring package.json

  if [[ $(git diff --shortstat 2>/dev/null | tail -n1) != "" ]]; then
    printf "${RED}%s${NC}\n" 'Please stash the changes in the current branch before calling fgdku!'
    cd - >/dev/null
    exit 1
  fi

  # Reset network adapter if network is not reachable
  if ! ping -c 1 1.1.1.1 >/dev/null; then
    sudo ifconfig en7 down
    sudo route flush
    sudo ifconfig en7 up
  fi

  curl -sX PUT -d 'state=on' 'https://ha.pombei.ro/api/webhook/gdk_update'

  rm -f .git/gc.log

  git fetch --jobs=10

  if [[ ! -S $GDK_ROOT/redis/redis.socket ]]; then
    # Work around issue with Redis socket disappearing by restarting the GDK
    printf "${YELLOW}%s${NC}\n" 'Redis socket disappeared, running gdk restart before update...'
    (cd $GDK_ROOT && gdk restart)
  fi

  mise install
  if ! command -v gdk >/dev/null; then
    # When ruby is upgraded, we don't have the gdk gem, so we need to reinstall it
    gem install gitlab-development-kit
  fi

  set -eo pipefail

  ORIGINAL_BRANCH="$(git_current_branch)"
  MIGRATIONS_DIFF="$(git diff --name-only --diff-filter=A master -- db/schema_migrations/)"
  BRANCH_MIGRATIONS=($(echo ${MIGRATIONS_DIFF} | xargs basename -a | sort -r))
  if [[ ${#BRANCH_MIGRATIONS[@]} -ne 0 ]]; then
    bin/spring binstub --all
    undo-migrations
    git restore bin/rake bin/rspec bin/rails bin/spring
    git stash push -m "Rolled back migration from ${ORIGINAL_BRANCH}"
  fi

  bin/spring stop || echo 'Spring not running'
  pkill spring || echo 'No remaining spring processes' # Ensure there are no leftover spring processes, this sometimes causes issues with the RubyMine debugger hanging trying to establish a connection
  git restore bin/rake bin/rspec bin/rails bin/spring

  printf "${YELLOW}%s${NC}\n" 'Updating GDK...'
  cd $GDK_ROOT
  mise x ruby -- gdk update

  cd gitlab
  git restore db/structure.sql
  sleep 5

  mise x ruby -- bundle install # Needed if Ruby version changed

  bin/spring binstub --all

  printf "${YELLOW}%s${NC}\n" 'Running simple test...'
  bundle exec rspec --failure-exit-code 0 --error-exit-code 1 ee/spec/services/ci/runners/stale_group_runners_prune_service_spec.rb ||
    gdk rake db:test:prepare

  printf "${YELLOW}%s${NC}\n" "Run any pending ClickHouse migrations..."
  gdk status clickhouse >/dev/null && gdk rake gitlab:clickhouse:migrate:main

  printf "${YELLOW}%s${NC}\n" 'Pruning local branches...'
  git fetch --jobs=10
  set +e
  git branch -vv | grep -E '(origin|security)/.*: gone]' | awk '{print $1}' | grep -v '^_.*$' | xargs git branch -D
  set -e
  printf "${GREEN}%s${NC}\n" "Done."

  printf "${YELLOW}%s${NC}\n" "Rebasing local branches..."
  git restore :/
  rebase-all
  git status --porcelain | grep '^UU ' >/dev/null && return 1
  printf "${GREEN}%s${NC}\n" "Done."

  if [[ "${ORIGINAL_BRANCH}" != "$(git_main_branch)" ]]; then
    if git rev-parse --abbrev-ref "${ORIGINAL_BRANCH}" >/dev/null 2>&1; then
      git switch "${ORIGINAL_BRANCH}"
    fi
  fi

  (
    cd $GDK_ROOT && mise x ruby -- bundle install && yes | gdk cleanup
    find gitlab/log -maxdepth 1 -size +10M -exec truncate -s 0 {} \;
  )

  set +e
  yes | gdk kill # Use kill instead of stop so that services are killed if they can't be stopped (e.g. postgresql)
  set -e
  # Ensure there are no orphaned sidekiq-cluster (https://gitlab.com/gitlab-org/gitlab-development-kit/-/issues/1455)
  pkill -lf 'sidekiq-cluster' || echo 'No stray sidekiq-cluster found'
  gdk start

  if [[ ${#BRANCH_MIGRATIONS[@]} -ne 0 ]]; then
    printf "${YELLOW}%s${NC}\n" 'Applying stash...'
    git stash pop
    bin/rails db:migrate RAILS_ENV=test
    scripts/regenerate-schema # Ensure we have a clean test DB with any branch migrations done
    git restore db/structure.sql
  fi

  cd - >/dev/null
)

curl -sX PUT -d 'state=off' 'https://ha.pombei.ro/api/webhook/gdk_update'
