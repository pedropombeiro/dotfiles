#!/usr/bin/env zsh

# Skip all this for non-interactive shells
[[ -z "$PS1" ]] && return

function zcompare() {
  if [[ -s ${1} && (! -s ${1}.zwc || ${1} -nt ${1}.zwc) ]]; then
    zcompile -R -- "${1}.zwc" "$1"
  fi
}

function source_file() {
  zcompare "$1"
  builtin source "$1"
}

# Source executable files, skipping yadm alternates (##), backups (~$), and compiled (.zwc)
function source_files() {
  local f
  for f; do
    [[ $f == *.zwc ]] && continue
    [[ $f == *"##"* ]] && continue
    [[ $f == *'~$'* ]] && continue
    test -x "$f" && source_file "$f"
  done
}

# Load config files from pre/, main dir, then post/ (in sorted order)
# Uses zsh globs instead of find for speed. Glob qualifiers:
#   (N) = nullglob (no error if no matches)
#   (-) = follow symlinks
#   (.) = regular files only
#   ${(o)...} = sort alphabetically
_load_settings() {
  local _dir="$1"
  local _files
  [[ -d "$_dir" ]] || return

  if [[ -d "$_dir/pre" ]]; then
    _files=("$_dir"/pre/**/*.*sh(N-.))
    (( ${#_files} )) && source_files ${(o)_files}
  fi

  _files=("$_dir"/*.*sh(N-.))
  (( ${#_files} )) && source_files ${(o)_files}

  if [[ -d "$_dir/post" ]]; then
    _files=("$_dir"/post/**/*.*sh(N-.))
    (( ${#_files} )) && source_files ${(o)_files}
  fi
}

_load_settings "$HOME/.shellrc/zshrc.d/configs"

# Use zinit's optimized compinit (runs after turbo-loaded plugins)
zicompinit; zicdreplay

# Load all files from .shell/rc.d directory
if [[ -d $HOME/.shellrc/rc.d ]]; then
  _rc_files=("$HOME"/.shellrc/rc.d/**/*.sh(N-.))
  (( ${#_rc_files} )) && source_files ${(o)_rc_files}
  unset _rc_files
fi

# aliases
source_file ~/.aliases.sh

unfunction zcompare source_file source_files

# autoload custom executable functions
_func_dir="${HOME}/.shellrc/zshrc.d/functions"
_function_files=("${_func_dir}"/*(N-.:t))  # :t = tail (basename only)
_function_files=(${_function_files:#*.zwc})
_function_files=(${_function_files:#*\#\#*})
_function_files=(${_function_files:#*~\$*})
if (( ${#_function_files} )); then
  fpath+=("${_func_dir}")
  autoload -Uz ${_function_files[@]}
fi
unset _func_dir _function_files
