#!/usr/bin/env zsh

# Skip all this for non-interactive shells
[[ -z "$PS1" ]] && return

function zcompare() {
  if [[ -s ${1} && (! -s ${1}.zwc || ${1} -nt ${1}.zwc) ]]; then
    zcompile ${1}
  fi
}

function source_file() {
  zcompare "$1"
  builtin source "$1"
}

# Source executable files, skipping yadm alternates (##), backups (~$), and compiled (.zwc)
function source_files() {
  local f
  for f; do
    [[ $f == *.zwc ]] && continue
    [[ $f == *"##"* ]] && continue
    [[ $f == *'~$'* ]] && continue
    test -x "$f" && source_file "$f"
  done
}

# Path to your oh-my-zsh installation.
export ZSH="$HOME/.oh-my-zsh"

# Skip oh-my-zsh's compinit since we run it ourselves earlier
skip_global_compinit=1

plugins=()

# Load config files from pre/, main dir, then post/ (in sorted order)
# Uses zsh globs instead of find for speed. Glob qualifiers:
#   (N) = nullglob (no error if no matches)
#   (-) = follow symlinks
#   (.) = regular files only
#   ${(o)...} = sort alphabetically
_load_settings() {
  local _dir="$1"
  local _files
  [[ -d "$_dir" ]] || return

  if [[ -d "$_dir/pre" ]]; then
    _files=("$_dir"/pre/**/*.*sh(N-.))
    (( ${#_files} )) && source_files ${(o)_files}
  fi

  _files=("$_dir"/*.*sh(N-.))
  (( ${#_files} )) && source_files ${(o)_files}

  if [[ -f "$ZSH/oh-my-zsh.sh" && -z "$INTELLIJ_ENVIRONMENT_READER" ]]; then
    source_file $ZSH/oh-my-zsh.sh
  fi

  if [[ -d "$_dir/post" ]]; then
    _files=("$_dir"/post/**/*.*sh(N-.))
    (( ${#_files} )) && source_files ${(o)_files}
  fi
}

autoload -Uz ${ZSH}/custom/plugins/zsh-defer/zsh-defer

# Run compinit early with -C flag (skip security check for speed)
# This makes compdef available for plugins that need it immediately
autoload -Uz compinit
compinit -C

_load_settings "$HOME/.shellrc/zshrc.d/configs"

# Load all files from .shell/rc.d directory
if [[ -d $HOME/.shellrc/rc.d ]]; then
  _rc_files=("$HOME"/.shellrc/rc.d/**/*.sh(N-.))
  (( ${#_rc_files} )) && source_files ${(o)_rc_files}
  unset _rc_files
fi

# aliases
source_file ~/.aliases.sh

unfunction zcompare source_file source_files

# autoload custom executable functions
_func_dir="${HOME}/.shellrc/zshrc.d/functions"
_function_files=("${_func_dir}"/*(N-.:t))  # :t = tail (basename only)
_function_files=(${_function_files:#*.zwc})
_function_files=(${_function_files:#*\#\#*})
_function_files=(${_function_files:#*~\$*})
if (( ${#_function_files} )); then
  fpath+=("${_func_dir}")
  autoload -Uz ${_function_files[@]}
fi
unset _func_dir _function_files

zsh-defer '
  sessions=$(tmux ls 2>&1)
  if [[ $? -eq 0 ]]; then
    if [[ $(echo $sessions | wc -l) -gt 1 ]]; then
      echo "Existing tmux sessions:"
      echo "$sessions"
    fi
  fi
'
